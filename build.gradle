plugins {
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "org.sonarqube" version "3.3"
	id 'java'
}

ext {
	setProperty('springfoxVersion', '3.0.0')
	setProperty('mapstructVersion', '1.4.2.Final')
	setProperty('openapiVersion', '1.5.10')
	setProperty('password4jVersion', '1.5.4')
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'java-library'
	apply plugin: 'jacoco'
	apply plugin: 'checkstyle'

	sourceCompatibility = '17'
	group = 'com.appmarket'
	version = projectVersion as Object

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
		annotationProcessor 'org.projectlombok:lombok'
		implementation "org.mapstruct:mapstruct:${mapstructVersion}"
		annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	dependencyManagement {
		imports {
			mavenBom("org.springframework.boot:spring-boot-dependencies:2.5.5")
		}
	}

	repositories {
		mavenCentral()
	}

	sonarqube {
		properties {
			property 'sonar.junit.reportPaths', 'build/test-results/test'
			property 'sonar.sources', 'src/main/java'
			property 'sonar.tests', 'src/test/java'
			property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco'
			property 'sonar.exclusions', [ '**/config/**', '**/exception/**', '**/specification/**' ]
		}
	}

	test {
		setJvmArgs([
				'-Dspring.profiles.active=test'
		])
		useJUnitPlatform()
	}

	test {
		finalizedBy jacocoTestReport // report is always generated after tests run
	}
	jacocoTestReport {
		dependsOn test // tests are required to run before generating the report
		reports {
			xml.enabled(true)
		}
	}

	checkstyle {
		toolVersion = '10.3'
		configFile = rootProject.file("config/checkstyle/checkstyle.xml")
	}
}

jar {
	enabled = false;
}

project(':adapters') {
	jar {
		enabled = false;
	}
}

project(':adapters:rest-adapter') {
	dependencies {
		implementation project(':application')
		implementation 'org.springframework.boot:spring-boot-starter-web'
		//implementation "io.springfox:springfox-boot-starter:${springfoxVersion}"
		implementation "org.springdoc:springdoc-openapi-ui:${openapiVersion}"
	}
}

project(':adapters:persistence-adapter') {
	dependencies {
		implementation project(':application')
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'org.flywaydb:flyway-core'
		runtimeOnly 'org.postgresql:postgresql'
		testRuntimeOnly 'com.h2database:h2'
	}
}

project(':application') {
	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-validation'
		implementation "com.password4j:password4j:${password4jVersion}"
	}
}

project(':configuration') {
	dependencies {
		implementation project(':application')
		implementation project(':adapters:rest-adapter')
		implementation project(':adapters:persistence-adapter')
		implementation 'org.springframework.boot:spring-boot-starter'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		runtimeOnly 'org.springframework.boot:spring-boot-devtools'
	}

	jar {
		archivesBaseName = "${rootProject.name}"
		enabled = false
	}

	sonarqube {
		skipProject = true
	}
}

sonarqube {
	properties {
		property 'sonar.language', 'java'
		property "sonar.projectKey", "romjunior_app-market-user-service"
		property "sonar.organization", "romjunior"
		property "sonar.host.url", "https://sonarcloud.io"
		property 'sonar.projectVersion', projectVersion
		property 'sonar.sourceEncoding', 'UTF-8'
	}
}